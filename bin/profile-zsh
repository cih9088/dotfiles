#!/usr/bin/env zsh

logfile=$(mktemp zsh_profile.XXXXXXXX)
echo "Logging to $logfile"
zshrc=$(readlink -f $HOME/.zshrc)
zshrcbak="$(dirname $zshrc)/$(basename $zshrc)-e"

sed -i -e '1s/^/# profile start \
zmodload zsh\/datetime \
setopt PROMPT_SUBST \
PS4='"'"'+$EPOCHREALTIME %N:%i> '"'"' \
\
exec 3>\&2 2>\&1 \
\
setopt XTRACE\
# profile end\
/' $zshrc


echo "# profile start
unsetopt XTRACE
exec 2>&3 3>&-
# profile end" >> $zshrc


typeset -a lines
typeset -i prev_time=0
typeset prev_command

while read line; do
  if [[ $line =~ '^.*\+([0-9]{10})\.([0-9]{6})[0-9]* (.+)' ]]; then
    integer this_time=$match[1]$match[2]

    if [[ $prev_time -gt 0 ]]; then
      time_difference=$(( $this_time - $prev_time ))
      lines+="$time_difference $prev_command"
    fi

    prev_time=$this_time

    local this_command=$match[3]
    if [[ ${#this_command} -le 80 ]]; then
      prev_command=$this_command
    else
      prev_command="${this_command:0:77}..."
    fi
  fi
done < <($(which zsh) -il -c exit)

sed -i -e '/# profile start/,/# profile end/d' $zshrc
rm -rf $zshrcbak || true


print -l ${(@On)lines} > $logfile
